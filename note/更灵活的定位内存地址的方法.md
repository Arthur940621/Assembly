# 更灵活的定位内存地址的方法

前面用 `[0]`、`[bx]` 的方法，在访问内存的指令中，定位内存单元的地址。

## `and` 和 `or` 指令

`and` 指令: 逻辑与指令，按位进行与运算。

例如指令:

```asm
mov al,01100011B
and al,00111011B
```

执行后: `al=00100011B`

通过该指令可将操作对象的相应位设为 `0`，其他位不变。

`or` 指令: 逻辑或指令，按位进行或运算。

例如指令:

```asm
moval，01100011B
oral，00111011B
```

`执行后: al=01111011B`

通过该指令可将操作对象的相应位设为 `1`，其他位不变。

## 关于 `ASCII` 码

一个文本编辑过程中，就包含着按照 `ASCII` 编码规则进行的编码和解码。在文本编辑过程中，按一下键盘的 `a` 键，就会在屏幕上看到 `a`。按下键盘的 `a` 键，这个按键的信息被送入计算机，计算机用 `ASCII` 码的规则对其进行编码，将其转化为 `61H` 存储在内存的指定空间中，文本编辑软件从内存中取出 `61H`，将其送到显卡上的显存中，工作在文本模式下的显卡，用 `ASCII` 码的规则解释显存中的内容，`61H` 被当作字符 `a`，显卡驱动显示器，将字符 `a` 的图像画在屏幕上。显卡在处理文本信息的时候，是按照 `ASCII` 码的规则进行的。这也就是说，如果要想在显示器上看到 `a`，就要给显卡提供 `a` 的 `ASCII` 码，将 `61H` 写入显存中。

## 以字符形式给出的数据

可以在汇编程序中，用 `'......'` 的方式指明数据是以字符的形式给出的，编译器将把它们转化为相对应的 `ASCII` 码。如下面的程序:

```asm
assume cs:code,ds:data
data segment
    db 'unIX'
    db 'foRK'
data ends

code segment
start:
    mov al,'a'
    mov bl,'b'
    mov ax,4c00h
    int 21h
code ends
end start
```

上面的源程序中：
`db 'unIX'` 相当于 `db 75H,6EH,49H,58H`。

`db 'foRK'` 相当于 `db 66H,6FH,52H,4BH`。

`mov bl, 'b'` 相当于 `mov al,62H`。

## 大小写转换的问题

下面考虑这样一个问题，在 `codesg` 中填写代码，将 `datasg` 中的第一个字符串转化为大写，第二个字符串转化为小写。

```asm
assume cs:codesg, ds:datasg
datasg segment
    db 'BaSic'
    db 'iNfOrMaTiOn'
datasg ends

codesg segment
start:

codesg ends
end start
```


首先分析一下，同一个字母的大写字符和小写字符对应的 `ASCII` 码是不同的。要改变一个字母的大小写，实际上就是要改变它所对应的 `ASCII` 码。

小写字母的 `ASCII` 码值比大写字母的 `ASCII` 码值大 `20H`。这样，如果将 `a` 的 `ASCII` 码值减去 `20H`，就可以得到 `A`，如果将 `A` 的 `ASCII` 码值加上 `20H` 就可以得到 `a`。

要注意的是，对于字符串 `BaSiC`，应只对其中的小写字母所对应的 `ASCII` 码进行减 `20H` 的处理，将其转为大写，而对其中的大写字母不进行改变，对于字符串 `iNfOrMaTiOn`，应只对其中的大写字母所对应的 `ASCII` 码进行加 `20H` 的处理，将其转为小写，而对于其中的小写字母不进行改变。程序必须要能够判断一个字母是大写还是小写。

```asm
assume cs:codesg, ds:datasg
datasg segment
    db 'BaSic'
    db 'iNfOrMaTiOn'
datasg ends

codesg segment
start:
    mov ax,datasg
    mov ds,ax
    mov bx,0
    mov cx,5
s:
    ; 如果 (al)>61H，则为小写字母的 ASCII 码，则: sub al,20H
    mov al,[bx]
    inc bx
    loop s
    ...
codesg ends
end start
```

判断将用到一些目前还没有学习到的指令。现在面临的问题是，用已学的指令来解决这个问题，则不能对字母的大小写进行任何判断。

可以看出，就 `ASCII` 码的二进制形式来看，除第 `5` 位（位数从 `0` 开始计算）外，大写字母和小写字母的其他各位都一样。大写字母 `ASCII` 码的第 `5` 位为 `0`，小写字母的第 `5` 位为 `1`。这样，一个字母，不管它原来是大写还是小写，将它的第 `5` 位置 `0`，它就必将变为大写字母，将它的第 `5` 位置 `1`，它就必将变为小写字母。在这个方法中，不需要在处理前判断字母的大小写。

```asm
assume cs:codesg, ds:datasg
datasg segment
    db 'BaSic'
    db 'iNfOrMaTiOn'
datasg ends

codesg segment
start:
    mov ax,datasg
    mov ds,ax
    mov bx,0
    mov cx,5
s:
    mov al,[bx]
    and al,11011111B
    mov [bx],al
    inc bx
    loop s

    mov bx,5
    mov cx,11
s0:
    mov al,[bx]
    or al,00100000B
    mov[bx],al
    inc bx
    loop s0

    mov ax,4c00h
    int 21h

codesg ends
end start
```

## 用 `[bx+idata]` 的方式进行数组的处理

有了 `[bx+idata]` 这种表示内存单元的方式，可以用更高级的结构来看待所要处理的数据。

在 `codesg` 中填写代码，将 `datasg` 中定义的第一个字符串转化为大写，第二个字符串转化为小写:

```asm
assume cs:codesg,ds:datasg
datasg segment
	db 'BaSiC'
	db 'MinIX'
datasg ends

codesg segment
	start:
codesg ends

end start
```

按照原来的方法，用 `[bx]` 的方式定位字符串的字符:

```asm
    mov ax,datasg
    mov ds,ax
    mov bx,0
    mov cx,5

s:
    mov al,[bx]
    and al,11011111b
    mov [bx],al
    inc bx
    loop s

    mov bx,5
    mov cx,5

s0:
    mov al,[bx]
    or al,00100000b
    mov [bx],al
    inc bx
    loop s0
```

`datasg` 段中的两个字符串，一个的起始地址为 `0`，另一个的起始地址为 `5`。可以将这两个字符串看作两个数组，一个从 0 地址开始存放，另一个从 5 开始存放。

可以用 `[0+bx]` 和 `[5+bx]` 的方式在同一个循环中定位这两个字符串中的字符。在这里，`0` 和 `5` 给定了两个字符串的起始偏移地址，`bx` 中给出了从起始偏移地址开始的相对地址。这两个字符串在内存中的起始地址是不一样的，但是，它们中的每一个字符，从起始地址开始的相对地址的变化是相同的:

```asm
    mov ax,datasg
    mov ds,ax
    mov bx,0
    mov cx,5
s:
    mov al,[bx]
    and al,11011111b
    mov [bx],al
    mov al,[5+bx]
    or al,00100000b
    mov [5+bx],al
    inc bx
    loop s
```

也可以写成下面的样子:

```asm
    mov ax,datasg
    mov ds,ax
    mov bx,0
    mov cx,5
s:
    mov al,0[bx]
    and al,11011111b
    mov 0[bx],al
    mov al,5[bx]
    or al,00100000b
    mov 5[bx],al
    inc bx
    loop s
```

`C` 语言描述上面的程序，大致是这样的:

```c
char a[5] = "BaSiC";
char b[5] = "MinIX";
main(){
    int i;
    i = 0;
    do {
        a[i] = a[i]&0xDF;
        b[i] = b[i]|0x20;
        i++;
    } while (i<5);
}
```

`C` 语言: `a[i]`，`b[i]`
汇编语言: `0[bx]`，`5[bx]`
通过比较可以发现，`[bx+iata]` 的方式为高级语言实现数组提供了便利机制。

## `SI` 和 `DI`

`si` 和 `di` 是 `8086CPU` 中和 `bx` 功能相近的寄存器，`si` 和 `di` 不能够分成两个 `8` 位寄存器来使用。

```asm
mov bx,0
mov ax,[bx]

mov si,0
mov ax,[si]

mov di,0
mov ax,[di]
```

```asm
mov bx,0
mov ax,[bx+123]

mov si,0
mov ax,[si+123]

mov di,0
mov ax,[di+123]
```

## `[bx+si]` 和 `[bx+di]`

`[bx(si或di)]` 和 `[bx(si或di)+idata]` 的方式来指明一个内存单元，还可以用更为灵活的方式: `[bx+si]` 和 `[bx+di]`。


`[bx+si]` 表示一个内存单元，它的偏移地址为 `(bx)+(si)`（即 `bx` 中的数值加上 `si` 中的数值）。

指令 `mov ax,[bx+si]` 的含义如下：

将一个内存单元的内容送入 `ax`，这个内存单元的长度为 `2` 字节，存放一个字，偏移地址为 `bx` 中的数值加上 `si` 中的数值，段地址在 `ds` 中。

数学化的描述为: `(ax)=((ds)*16+(bx)+(si)`)

该指令也可以写成如下格式(常用):

```asm
mov ax,[bx][si]
```

## `[bx+si+idata]` 和 `[bx+di+idata]`

`[bx+si+idata]` 表示一个内存单元，它的偏移地址为 `(bx)+(si)+idata`（即 `bx` 中的数值加上 `si` 中的数值再加上 `idata`）。

指令 `mov ax,[bx+si+idata]` 的含义如下:

将一个内存单元的内容送入 `ax`，这个内存单元的长度为 `2` 字节，存放一个字，偏移地址为 `bx` 中的数值加上 `si` 中的数值再加上 `idata`，段地址在 `ds` 中。

数学化的描述为: `(ax)=((ds)*16+(bx)+(si)+idata)`

该指令也可以写成如下的格式:

```asm
mov ax,[bx+200+si]
mov ax,[200+bx+si]
mov ax,200[bx][si]
mov ax,[bx].200[si]
mov ax,[bx][si].200
```

## 不同的寻址方式的灵活应用

通过前面几种定位内存地址的方法，可以发现:

- `[idata]` 用一个常量来表示地址，可用于直接定位一个内存单元
- `[bx]` 用一个变量来表示内存地址，可用于间接定位一个内存单元
- `[bx+idata]` 用一个变量和常量表示地址，可在一个起始地址的基础上用变量间接定位一个内存单元
- `[bx+si]` 用两个变量表示地址
- `[bx+si+idata]` 用两个变量和一个常量表示地址

进行二重循环时，用一个循环计数器，会造成在进行内存循环的时候，覆盖了外层循环的循环计数值。多用了一个计数器又不可能，因为 `loop` 指令默认 `cx` 为循环计数器。这个时候需要在每次开始内存循环的时候，将外层循环的 `cx` 中的数值保存起来，在执行外层循环 `loop` 指令前，再恢复外层循环的 `cx` 的数值。可以用寄存器 `dx` 来临时保存 `cx` 中的数值，如果在内存循环中，`dx` 寄存器也被使用，`CPU` 中寄存器的数量毕竟是有限的。而且每个程序中可使用的寄存器都不一样。那么可以使用内存。可以考虑将需要暂存的数据放到内存单元中，需要使用的时候，再从内存单元中恢复。如果需要保存多个数据的时候，必须要记住数据存放在哪个单元中。这样程序就很容易混乱。一般来说，在需要暂存数据的时候，都应该使用栈。